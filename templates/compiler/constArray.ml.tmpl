(*-----------------------------------------------------------------------------
 *
 * プログラムを通して中身が変化しない配列（constArray）を展開
 *
 *-----------------------------------------------------------------------------*)
open KNormal

let memi x env =
	try (match M.find x env with Int(_) -> true | _ -> false)
	with Not_found -> false
let memea x env =
	try (match M.find x env with ExtArray _ -> true | _ -> false)
	with Not_found -> false

let findi x env = (match M.find x env with Int i -> i | _ -> raise Not_found)
let findea x env = (match M.find x env with ExtArray x -> x | _ -> raise Not_found)

type state = Null | Init of t | NotConst

let extarray = ref M.empty

let getstate x i =
	try 
		match M.find x !extarray with
			| None -> NotConst
			| Some(l) -> try List.nth l i with nth -> Null
	with Not_found -> Null


let rec assign n x = function
	| [] -> if n = 0 then [x] else Null :: assign (n-1) x []
	| hd :: tl -> if n = 0 then x :: tl else hd :: assign (n-1) x tl
    
let setstate x i y=
	try 
		match M.find x !extarray with
			| None -> ()
			| Some(l) -> extarray := M.add x (Some (assign i y l)) !extarray
	with Not_found -> extarray := M.add x (Some (assign i y [])) !extarray

let g constarray env =
	let rec g' env = function
		| IfEq (x, y, e1, e2) -> IfEq (x, y, g' env e1, g' env e2)
		| IfLE (x, y, e1, e2) -> IfLE (x, y, g' env e1, g' env e2)
		| LetRec ({body = e1} as fundef, e2) -> LetRec({fundef with body = g' env e1}, g' env e2)
		| LetTuple (xts, y, e) -> LetTuple (xts, y, g' env e)
		| Let((x, t), e1, e2) ->
			let e1 = g' env e1 in
			let e2 = g' (M.add x e1 env) e2 in
			Let((x, t), e1, e2)
		| Put (x, y, z) as e when memea x env & memi y env ->
			let x' = ((findea x env), (findi y env)) in
			if List.mem_assoc x' constarray then Unit 	else e
		| Get (x, y) as e when memea x env & memi y env ->
			let x' = ((findea x env), (findi y env)) in
			if List.mem_assoc x' constarray then List.assoc x' constarray else e
		| e -> e in
	g' env

let isconst = function Unit | Int _ | Float _ -> true | _ -> false

let rec setExtarray env = function
	| ExtArray(a) -> (* 自由な出現は定数でないとみなす *)
		extarray := M.add a None !extarray
	| IfEq (_, _, e1, e2) | IfLE (_, _, e1, e2)
	| LetRec ({body = e1}, e2) -> setExtarray env e1; setExtarray env e2
	| App (_,ys) | ExtFunApp (_,ys) | Tuple (ys) ->
		List.iter (
			fun x ->
				if memea x env then
					extarray := M.add (findea x env) None !extarray
				else
					()
		) ys
	| LetTuple (xts, y, e) -> setExtarray env e
	(* 変数に束縛される外部配列は記憶 *)
	| Let((x, _), (ExtArray _ as e1), e2) -> setExtarray (M.add x e1 env) e2
	| Let((x, _), e1, e2) ->
		setExtarray env e1; setExtarray (M.add x e1 env) e2
	| Put(x, y, z) when memea x env & memi y env & M.mem z env ->
		let x' = findea x env in
		let i = findi y env in
		let z' = M.find z env in
		(match getstate x' i with
	| Null when isconst z' ->
	    setstate x' i (Init z')
	| Null ->
	    setstate x' i NotConst
	| Init t when t <> z' ->
	    setstate x' i NotConst
	| _ -> ())
	(* 値が不定 *)
	| Put(x, y, z) when memea x env & memi y env -> setstate (findea x env) (findi y env) NotConst
	(* 代入される場所が不定 *)
	| Put(x, y, z) when memea x env -> 	extarray := M.add (findea x env) None !extarray
	| _ -> ()

let makeConstarray () =
	M.fold (
		fun array sta list ->
			match sta with
				| None -> list
				| Some(stas) ->
					fst (
						List.fold_left (
							fun (l,n) x ->
								match x with
									| Init(e) -> ((array,n),e) :: l, n+1
									| _ -> (l,n+1)
						) (list,0) stas
					)
	) !extarray []

let f e =
	extarray := M.empty;
	setExtarray M.empty e;
	let constarray = makeConstarray () in
	List.iter (
		fun ((a,n),e)->
			match e with
				| Unit -> Format.eprintf "Const : %s.(%d) -> Unit@." a n
				| Int i -> Format.eprintf "Const : %s.(%d) -> Int(%d)@." a n i
				| Float f -> Format.eprintf "Const : %s.(%d) -> Float(%f)@." a n f
				| _ -> assert false
	) constarray;
	g constarray M.empty e
